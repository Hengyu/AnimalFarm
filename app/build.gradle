plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.diffplug.spotless'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat().aosp()
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    kotlin {
        target '**/*.kt'
        ktlint()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'xml', {
        target '**/*.xml'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.artlvr.animalfarm"
        minSdk 31
        targetSdk 33
        versionCode 2
        versionName "2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = '1.4.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    // https://stackoverflow.com/a/42653151/4402255
    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'com.artlvr.animalfarm'
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation 'com.datadoghq:dd-sdk-android:1.19.0'
    implementation 'com.google.accompanist:accompanist-pager:0.28.0'
    implementation 'com.google.accompanist:accompanist-pager-indicators:0.28.0'
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.28.0'
    implementation 'com.google.dagger:hilt-android:2.46'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    kapt 'com.google.dagger:hilt-android-compiler:2.46'
    testImplementation 'io.mockk:mockk:1.13.5'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}
